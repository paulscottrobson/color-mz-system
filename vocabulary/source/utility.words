; ***************************************************************************************
; ***************************************************************************************
;
;		Name : 		utility.words
;		Author :	Paul Robson (paul@robsons.org.uk)
;		Date : 		17th November 2018
;		Purpose :	Miscellaneous words.
;
; ***************************************************************************************
; ***************************************************************************************

; @generator break 
		db 		$DD,$01
; @end

; @word sys.info
		ex 		de,hl
		ld 		hl,SystemInformationTable
		ret
; @end

; @generator ; 
		ret
; @end
		
; @word inkey
		ex 		de,hl
		call 	IOScanKeyboard
		ld 		l,a
		ld 		h,0
		ret
; @end

; @word halt
HaltZ80:
		di
		halt
		jr 		HaltZ80
; @end

; @word copy
		ld 		a,b 								; nothing to do.
		or 		c
		ret 	z

		push 	bc
		push 	de
		push 	hl

		xor 	a 									; find direction. 
		sbc 	hl,de
		ld 		a,h
		add 	hl,de
		bit 	7,a 								; if +ve use LDDR
		jr 		z,__copy2

		ex 		de,hl 								; LDIR etc do (DE) <- (HL)
		ldir
__copyExit:
		pop 	hl
		pop 	de
		pop 	bc
		ret

__copy2:		
		add 	hl,bc 								; add length to HL,DE, swap as LDDR does (DE) <- (HL)
		ex 		de,hl
		add 	hl,bc
		dec 	de 									; -1 to point to last byte
		dec 	hl
		lddr 
		jr 		__copyExit		
; @end

; @word fill
		ld 		a,b 								; nothing to do.
		or 		c
		ret 	z
		push	bc
		push 	hl
		
__fill1:ld 		(hl),e
		inc 	hl
		dec 	bc
		ld 		a,b
		or 		c
		jr 		nz,__fill1

		pop 	hl
		pop 	bc
		ret
; @end
